#!/usr/bin/env perl

=head1 NAME

    zd - ZabbixCLI Dashboard

=head1 SYNOPSIS

    zd [-p] [-l n] [-g group_name,group_name...]
    zd [-p] [-l n] [-s screen_name,screen_name...]

=head2 Options

=over 4

=item -h

This message.

=item -p

Priority output

=item -l

Number of last tickets

=item -g

Group list

=item -s

Screen list

=back

=cut

use warnings;
use strict;
use Data::Dumper;
use FindBin '$RealBin';
use lib "$RealBin/../lib";
use config;
use Net::Zabbix;
use Getopt::Long;
use Pod::Usage;

my $resourcetype_dict = {
	GRAPH => 0,
	SIMPLE_GRAPH => 1,
	MAP => 2,
	PLAIN_TEXT => 3,
	HOSTS_INFO => 4,
	TRIGGERS_INFO => 5,
	SERVER_INFO => 6,
	CLOCK => 7,
	SCREEN => 8,
	TRIGGERS_OVERVIEW => 9,
	DATA_OVERVIEW => 10,
	URL => 11,
	HISTORY_OF_ACTIONS => 12,
	HISTORY_OF_EVENTS => 13,
	LATEST_HOST_GROUP_ISSUES => 14,
	SYSTEM_STATUS => 15,
	LATEST_HOST_ISSUES => 16,
	SIMPLE_GRAPH_PROTOTYPE => 19,
	GRAPH_PROTOTYPE => 20,
};

my $config = config->new("$RealBin/../etc/config");
my @dashboard_colors = $config->{dashboard_colors} ? split(/\s+/, $config->{dashboard_colors}) : (0, 96, 93, 91, 31, 41);

Getopt::Long::Configure("bundling");
GetOptions(
    "help|h|?" => sub { usage() },
    "verbose|v+" => \$config->{verbose},
    "debug" => \$config->{debug},
    "limit|l=s" => \$config->{dashboard_limit},
    "no-colors|c" => \$config->{dashboard_no_colors},
    "priority|p" => \$config->{dashboard_priority_output},
    "groups|g=s" => \$config->{dashboard_groups_list},
    "screen|s=s" => \$config->{dashboard_screen_list},
    "show-expressions|e" => \$config->{dashboard_screen_show_expressions},
);

$config->{dashboard_limit} ||= 20;

my $z= Net::Zabbix->new(
    $config->{'zabbix-url'},
    $config->{'zabbix-username'},
    $config->{'zabbix-password'},
);

if ($config->{dashboard_screen_list}) {
    foreach my $screen_query (split /[\s,:]/, $config->{dashboard_screen_list}) {
        my $screens = $z->get('screen', {
            #output => "extend",
            selectScreenItems => "extend",
            search => {
                name => $screen_query,
            },
        });
        # print Dumper($screens);exit;
        zabbix_error($screens);

        foreach my $screen (@{ $screens->{result} }) {
            my $screen_name = $screen->{name};

            foreach my $screen_item (@{ $screen->{screenitems} }) {
				next if $screen_item->{resourcetype} ne $resourcetype_dict->{LATEST_HOST_GROUP_ISSUES};
                my $groupid = $screen_item->{resourceid};

                my $groups = $z->get('hostgroup', {
                    groupids => $groupid,
                });

                zabbix_error($groups);

                if ($groups->{result} and $#{ $groups->{result} } >=0) {
                    my $group_name = $groups->{result}[0]->{name};
                    print "[$screen_name] $group_name:\n";
                    show_triggers($groupid);
                }
            }
        }
    }
} elsif ($config->{dashboard_groups_list}) {
    foreach my $group_name (split /[\s,:]/, $config->{dashboard_groups_list}) {
        my $groups = $z->get('hostgroup', {
            search => {
                name => $group_name,
            },
            searchWildcardsEnabled => 1,
        });

        zabbix_error($groups);

        foreach my $group (@{ $groups->{result} }) {
            print $group->{name} . ":\n";
            show_triggers($group->{groupid});
        }
    }
} else {
    show_triggers();
}

sub show_triggers {
    my ($groupid) = @_;
    my $triggers = $z->get('trigger', {
        skipDependent => 1,
        monitored => 1,
        filter => {
            value => 1,
        },
        limit => $config->{dashboard_limit},
        expandDescription => 1,
        expandExpression => 1,
        selectHosts => [ "hostis", "name" ],
        sortfield => $config->{dashboard_priority_output} ? [ "priority", "lastchange" ] : [ "lastchange" ],
        sortorder => "DESC",
        groupids => $groupid,
    });

# print Dumper($triggers);exit;

    zabbix_error($triggers);

    my $max_hostname_len = 0;
    foreach (@{ $triggers->{result} }) {
        $max_hostname_len = length($_->{hosts}[0]->{name}) if $max_hostname_len < length($_->{hosts}[0]->{name});
    }

    foreach my $trigger (@{ $triggers->{result} }) {
        next if ! $trigger->{hosts} or ! $trigger->{hosts}[0]->{name};
        print "\t" if $groupid;

        if ($config->{dashboard_no_colors}) {
            printf("%-${max_hostname_len}s : %s : (%s)\n",
                        $trigger->{hosts}[0]->{name},
                        $trigger->{description},
                        get_age($trigger->{lastchange}));
        } else {
            my $color = "\033[" . $dashboard_colors[$trigger->{priority}] . "m";
            printf("\033[0m%-${max_hostname_len}s\033[m : $color%s\033[m : \033[90m(%s)\033[m\n",
                        $trigger->{hosts}[0]->{name},
                        $trigger->{description},
                        get_age($trigger->{lastchange}));
        }

        if ($config->{dashboard_screen_show_expressions}) {
            print "\t" if $groupid;
            print "\033[90m" unless $config->{dashboard_no_colors};
            print "\t" . $trigger->{expression} . "\n";
            print "\033[m" unless $config->{dashboard_no_colors};
        }
    }
}

sub zabbix_error {
    my ($obj) = @_;
    unless ($obj->{result}) {
        if ($obj->{error}) {
            print $obj->{error}->{message} . "\n" . $obj->{error}->{data} . "\n";
            exit 1;
        } else {
            die "unknown error";
        }
    }
}

sub get_age {
    my ($lastchange) = @_;
    require Time::Ago;
    my $age = Time::Ago->in_words(time - $lastchange);
    utf8::encode($age);
    return $age;
}

sub usage {
    pod2usage(1);
    exit;
}
